dnl Process this file with autoconf to produce a configure script

dnl Initialization
AC_PREREQ(2.50)
AC_INIT
AC_CONFIG_SRCDIR(rpc2-src/rpc2.private.h)
CODA_SETUP_BUILD
AM_INIT_AUTOMAKE(rpc2, 1.18)
AM_CONFIG_HEADER(config.h)

dnl For library version updates, follow these steps in order.
dnl - when any library source has changed, increment first
dnl - when interfaces were added/removed changed, increment second and set
dnl   first to 0
dnl - if any interfaces were added, increment third
dnl - if any interfaces were removed, set third to 0
CODA_LIBRARY_VERSION(9, 4, 1)

CODA_OPTION_LWP

dnl Checks for programs.
AC_PROG_CC
CODA_PROG_NATIVECC
AC_PROG_CPP
AC_CHECK_PROG(AR, ar, ar)
export AR
AM_PROG_LEX
AC_PROG_YACC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AM_PROG_LIBTOOL
AC_CHECK_PROG(CP, cp, cp)
AC_CHECK_PROG(PERL, perl, perl)

dnl Checks for libraries.
CODA_FIND_LIB(lwp, [#include <lwp/lwp.h>], [LWP_Init(0,0,0)])
CODA_CHECK_LIBTERMCAP
CODA_CHECK_READLINE
AC_SEARCH_LIBS(inet_ntoa, nsl)
AC_SEARCH_LIBS(socket, socket)

dnl For playing with the _pt version...
if test ${with_LWP_PT}p = nop ; then
  AC_CHECK_LIB(lwp, LWP_Init, [LIBLWP="-llwp"],
               [AC_MSG_ERROR("Could not locate liblwp")])
else
  AC_CHECK_LIB(pthread, pthread_create, [LIBPTHREAD="-lpthread"])
  AC_CHECK_LIB(lwp_pt, LWP_Init, [LIBLWP="-llwp_pt"],
               [AC_MSG_ERROR("Failed to locate liblwp_pt")], [${LIBPTHREAD}])
  CPPFLAGS="-D_REENTRANT ${CPPFLAGS}"
fi
AC_SUBST(LIBLWP)
AC_SUBST(LIBPTHREAD)

dnl Checks for header files.
AC_CHECK_HEADERS(sys/stream.h arpa/inet.h arpa/nameser.h)

dnl Checks for typedefs.

dnl Checks for compiler characteristics.
CODA_DARWIN_BROKEN_CPP_WORKAROUND

dnl Checks for library functions.
AC_CHECK_FUNCS(inet_aton inet_ntoa ffs iopen)
AC_FUNC_SELECT_ARGTYPES

dnl Checks for system services.
AC_CYGWIN
AC_EXEEXT

if test ${with_LWP_PT}p != nop ; then
  AC_MSG_WARN(["Building against pthreaded lwp libraries, *experimental*"])
fi

dnl Generate output files
AC_CONFIG_FILES(Makefile pkgs/Makefile pkgs/pkg-bsd.sh pkgs/rpc2.spec pkgs/rpc2-dos.spec pkgs/rpc2-win95.spec pkgs/rpc2-nt.spec debian/Makefile include/Makefile include/rpc2/Makefile rpc2-src/Makefile rp2gen/Makefile fail/Makefile)
AC_OUTPUT

