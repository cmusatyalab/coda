dnl Process this file with autoconf to produce a configure script

dnl Initialization
AC_PREREQ(2.50)
AC_INIT
AC_CONFIG_SRCDIR(rvm/rvm_private.h)
CODA_SETUP_BUILD
AM_INIT_AUTOMAKE(rvm, 1.10)
AC_CONFIG_HEADERS(config.h)

dnl For library version updates, follow these steps in order.
dnl - when any library source has changed, increment first
dnl - when interfaces were added/removed changed, increment second and set
dnl   first to 0
dnl - if any interfaces were added, increment third
dnl - if any interfaces were removed, set third to 0
CODA_LIBRARY_VERSION(0, 3, 2)

CONFIG_DATE=`date +"%a, %d %b %Y %T %z"`
AC_SUBST(CONFIG_DATE, "$CONFIG_DATE", [Date when configure was last run])

CODA_OPTION_LWP

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_CHECK_PROG(AR, ar, ar)
export AR
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AM_PROG_LIBTOOL

dnl Checks for libraries.
AC_SEARCH_LIBS(fdatasync, rt)

CODA_FIND_LIB(lwp, [#include <lwp/lwp.h>], [LWP_Init(0,0,0)])

dnl For playing with the _pt version...
if test ${with_LWP_PT}p = nop ; then
  AC_CHECK_LIB(lwp, LWP_Init, [LIBLWP="-llwp"],
               [AC_MSG_ERROR("Could not locate liblwp")])
else
  AC_CHECK_LIB(pthread, pthread_create, [LIBPTHREAD="-lpthread"])
  AC_CHECK_LIB(lwp_pt, LWP_Init, [LIBLWP="-llwp_pt"],
               [AC_MSG_ERROR("Failed to locate liblwp_pt")], [${LIBPTHREAD}])
  CPPFLAGS="-D_REENTRANT ${CPPFLAGS}"
fi
AC_SUBST(LIBLWP)
AC_SUBST(LIBPTHREAD)

dnl Checks for header files.
dnl Checks for typedefs.
dnl Checks for compiler characteristics.

dnl Checks for library functions.
AC_CHECK_FUNCS(strerror fdatasync)
AC_FUNC_MMAP

dnl Checks for system services.

if test ${with_LWP_PT}p != nop ; then
  AC_MSG_WARN(["Building against pthreaded lwp libraries, *experimental*"])
fi

dnl Generate output files
AC_CONFIG_FILES(Makefile include/Makefile include/rvm/Makefile rvm/Makefile seg/Makefile rds/Makefile tests/Makefile pkgs/Makefile pkgs/pkg-bsd.sh pkgs/rvm.spec pkgs/rvm-nt.spec pkgs/rvm-dos.spec pkgs/rvm-win95.spec debian/Makefile debian/changelog)
AC_OUTPUT
